;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;; Krieg-Tictactoe: an imperfect-information version of Tic Tac Toe, where
;;; players simultaneously chose a spot. Players do not receive information
;;; on which spot was chosen by the opponent. If the same spot was chosen, or
;;; the player tried to mark a spot, marked by the opponent, the move is lost.
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; Roles
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(role white)
(role black)

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;; BASE
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(base (location ?v ?x ?y))
(base (tried ?r ?x ?y))
(base (step ?s))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;; INIT Relations
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(init (location b 0 0))
(init (location b 0 1))
(init (location b 0 2))
(init (location b 1 0))
(init (location b 1 1))
(init (location b 1 2))
(init (location b 2 0))
(init (location b 2 1))
(init (location b 2 2))
(init (step 1))


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;; LEGAL Relations
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(<= (legal ?p (mark ?m ?n))
   (role ?p)
   (true (location ?c ?m ?n))
   (distinct ?c ?p)
   (not (true (tried ?p ?m ?n))))

(<= doublehit
   (does white (mark ?m ?n))
   (does black (mark ?m ?n)))

(<= (validmove ?p ?m ?n)
   (does ?p (mark ?m ?n))
   (true (location b ?m ?n))
   (not doublehit))

(<= (marked ?m ?n)
   (does ?p (mark ?m ?n))
   (not doublehit))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;; NEXT Relations
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(<= (next (location ?p ?m ?n))
   (validmove ?p ?m ?n))

(<= (next (location ?c ?m ?n))
   (true (location ?c ?m ?n))
   (distinct ?c b))

(<= (next (location b ?m ?n))
   (true (location b ?m ?n))
   (not (marked ?m ?n)))

(<= (next (tried ?p ?m ?n))
   (does ?p (mark ?m ?n))
   (not (validmove ?p ?m ?n)))

(<= (next (tried ?p ?m ?n))
   (true (tried ?p ?m ?n)))

(<= (next (step ?n))
   (true (step ?m))
   (succ ?m ?n))


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;; SEES Relations
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(<= (sees ?p (yougotit ?m ?n))
   (validmove ?p ?m ?n))

(<= (sees ?p (mark ?m ?n))
   (does ?p (mark ?m ?n))
   (not (validmove ?p ?m ?n)))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;; TERMINAL Relations
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(<= terminal
   (line white))

(<= terminal
   (line black))

(<= terminal
   (not open))

(<= terminal
   (true (step 30)))

(<= (line ?c)
   (true (location ?c ?m 0))
   (true (location ?c ?m 1))
   (true (location ?c ?m 2)))

(<= (line ?c)
   (true (location ?c 0 ?n))
   (true (location ?c 1 ?n))
   (true (location ?c 2 ?n)))

(<= (line ?c)
   (true (location ?c 0 0))
   (true (location ?c 1 1))
   (true (location ?c 2 2)))

(<= (line ?c)
   (true (location ?c 0 2))
   (true (location ?c 1 1))
   (true (location ?c 2 0)))

(<= open
   (true (location b ?m1 ?n1))
   (not (true (tried white ?m1 ?n1)))
   (true (location b ?m2 ?n2))
   (not (true (tried black ?m2 ?n2))))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;; GOAL Relations
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(<= (goal white 100)
   (line white))
  
(<= (goal white 50)
   (not (line white))
   (not (line black)))
  
(<= (goal white 0)
   (line black))

(<= (goal black 100)
   (line black))
  
(<= (goal black 50)
   (not (line white))
   (not (line black)))
  
(<= (goal black 0)
   (line white))

(<= (tried ?r ?m  ?n)
    fail)