;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;; Maze Game
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;; ROLE
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(role robot)

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;; BASE
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(base (location ?o ?x ?y))
(base (step ?s))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;; INIT
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; Y
;   ---------------------
; 0 | W |   |   |   |   |
;   ---------------------
; 1 |   | W |   | W |   |
;   ---------------------
; 2 |   |   |   | W | G |
;   ---------------------
; 3 |   | W |   |   | W |
;   ---------------------
; 4 | R | W | W |   |   |
;   ---------------------
;     0   1   2   3   4     X

(init (location robot 0 4))
(init (location wall 0 0))
(init (location wall 1 1))
(init (location wall 1 3))
(init (location wall 1 4))
(init (location wall 2 4))
(init (location wall 3 2))
(init (location wall 3 1))
(init (location wall 4 3))
(init (location gold 4 2))
(init (step 1))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;; NEXT
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;


(<= (next (location wall ?x ?y))
    (location wall ?x ?y))

(<= (next (location gold ?x ?y))
    (location gold ?x ?y))

(<= (next (location robot ?x ?yup))
    (does robot move_up)
    (location robot ?x ?y)
    (succ ?yup ?y))

(<= (next (location robot ?x ?ydown))
    (does robot move_down)
    (location robot ?x ?y)
    (succ ?y ?ydown))

(<= (next (location robot ?xright ?y))
    (does robot move_right)
    (location robot ?x ?y)
    (succ ?x ?xright))

(<= (next (location robot ?xleft ?y))
    (does robot move_left)
    (location robot ?x ?y)
    (succ ?xleft ?x))


(<= (next (step ?s2))
    (step ?s)
    (succ ?s ?s2))

(succ 1 2)
(succ 2 3)
(succ 3 4)
(succ 4 5)
(succ 5 6)
(succ 6 7)
(succ 7 8)
(succ 8 9)
(succ 9 10)
(succ 10 11)
(succ 11 12)
(succ 12 13)
(succ 13 14)
(succ 14 15)
(succ 15 16)
(succ 16 17)
(succ 17 18)
(succ 18 19)
(succ 19 20)

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;; LEGAL
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(<= (legal robot move_up)
    (location robot ?x ?y)
    (> ?y 0)
    (succ ?yup ?y)
    (not (location wall ?x ?yup)))

(<= (legal robot move_down)
    (location robot ?x ?y)
    (< ?y 4)
    (succ ?y ?ydown)
    (not (location wall ?x ?ydown)))

(<= (legal robot move_right)
    (location robot ?x ?y)
    (< ?x 4)
    (succ ?x ?xright)
    (not (location wall ?xright ?y)))

(<= (legal robot move_left)
    (location robot ?x ?y)
    (> ?x 0)
    (succ ?xleft ?x)
    (not (location wall ?xleft ?y)))


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;; EFFECTS
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
(<= (effect robot ?move (list (does robot ?move) (does robot ?prev_move) (does robot ?next_move)) (list 0.8 0.1 0.1))
    (prev_move ?move ?prev_move)
    (next_move ?move ?next_move)
    (legal robot ?move)
    (legal robot ?prev_move)
    (legal robot ?next_move))

(<= (effect robot ?move (list (does robot ?move) (does robot ?prev_move)) (list 0.85 0.15))
    (prev_move ?move ?prev_move)
    (next_move ?move ?next_move)
    (legal robot ?move)
    (legal robot ?prev_move)
    (not (legal robot ?next_move)))

(<= (effect robot ?move (list (does robot ?move) (does robot ?next_move)) (list 0.85 0.15))
    (prev_move ?move ?prev_move)
    (next_move ?move ?next_move)
    (legal robot ?move)
    (legal robot ?next_move)
    (not (legal robot ?prev_move)))

(<= (effect robot ?move (list (does robot ?move)) (list 1))
    (prev_move ?move ?prev_move)
    (next_move ?move ?next_move)
    (legal robot ?move)
    (not (legal robot ?prev_move))
    (not (legal robot ?next_move)))

(prev_move move_up move_left)
(prev_move move_right move_up)
(prev_move move_down move_right)
(prev_move move_left move_down)
(<= (next_move ?move ?next_move)
    (prev_move ?next_move ?move))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;; GOAL
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(<= (goal robot 100)
    (location robot ?x ?y)
    (location gold ?x ?y))

(<= (goal robot 0)
    (location robot ?x ?y)
    (location gold ?xg ?yg)
    (or (distinct ?x ?xg) (distinct ?y ?yg)))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;; TERMINAL
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(<= terminal
    (step 20))

(<= terminal
    (location robot ?x ?y)
    (location gold ?x ?y))
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;