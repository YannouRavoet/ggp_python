(role white)
(role black)

(init (cell 1 1 b))
(init (cell 1 2 b))
(init (cell 1 3 b))
(init (cell 2 1 b))
(init (cell 2 2 b))
(init (cell 2 3 b))
(init (cell 3 1 b))
(init (cell 3 2 b))
(init (cell 3 3 b))
(init (step 1))

(<= (legal ?p (mark ?m ?n))
   (role ?p)
   (true (cell ?m ?n ?c))
   (distinct ?c ?p)
   (not (true (tried ?p ?m ?n))))

(<= doublehit
   (does white (mark ?m ?n))
   (does black (mark ?m ?n)))

(<= (validmove ?p ?m ?n)
   (does ?p (mark ?m ?n))
   (true (cell ?m ?n b))
   (not doublehit))

(<= (marked ?m ?n)
   (does ?p (mark ?m ?n))
   (not doublehit))

(<= (next (cell ?m ?n ?p))
   (validmove ?p ?m ?n))

(<= (next (cell ?m ?n ?c))
   (true (cell ?m ?n ?c))
   (distinct ?c b))

(<= (next (cell ?m ?n b))
   (true (cell ?m ?n b))
   (not (marked ?m ?n)))

(<= (next (tried ?p ?m ?n))
   (does ?p (mark ?m ?n))
   (not (validmove ?p ?m ?n)))

(<= (next (tried ?p ?m1 ?n1))
   (true (tried ?p ?m1 ?n1))
   (does ?p (mark ?m2 ?n2))
   (not (validmove ?p ?m2 ?n2)))

(<= (next (step ?n))
   (true (step ?m))
   (succ ?m ?n))

(<= (sees ?p (yougotit ?m ?n))
   (validmove ?p ?m ?n))

(<= (sees ?p (mark ?m ?n))
   (does ?p (mark ?m ?n))
   (not (validmove ?p ?m ?n)))

(<= terminal
   (line white))

(<= terminal
   (line black))

(<= terminal
   (not open))

(<= terminal
   (true (step 30)))

(<= (line ?c)
   (true (cell ?m 1 ?c))
   (true (cell ?m 2 ?c))
   (true (cell ?m 3 ?c)))

(<= (line ?c)
   (true (cell 1 ?n ?c))
   (true (cell 2 ?n ?c))
   (true (cell 3 ?n ?c)))

(<= (line ?c)
   (true (cell 1 1 ?c))
   (true (cell 2 2 ?c))
   (true (cell 3 3 ?c)))

(<= (line ?c)
   (true (cell 1 3 ?c))
   (true (cell 2 2 ?c))
   (true (cell 3 1 ?c)))

(<= open
   (true (cell ?m1 ?n1 b))
   (not (true (tried white ?m1 ?n1)))
   (true (cell ?m2 ?n2 b))
   (not (true (tried black ?m2 ?n2))))

(<= (goal white 100)
   (line white))
  
(<= (goal white 50)
   (not (line white))
   (not (line black)))
  
(<= (goal white 0)
   (line black))

(<= (goal black 100)
   (line black))
  
(<= (goal black 50)
   (not (line white))
   (not (line black)))
  
(<= (goal black 0)
   (line white))

(<= (tried ?r ?m  ?n)
    fail)