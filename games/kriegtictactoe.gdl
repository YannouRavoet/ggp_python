;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;; Krieg-Tictactoe: an imperfect-information version of Tic Tac Toe, where
;;; players simultaneously chose a spot. Players do not receive information
;;; on which spot was chosen by the opponent. If the same spot was chosen, or
;;; the player tried to mark a spot, marked by the opponent, the move is lost.
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; Roles
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(role white)
(role black)

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;; BASE
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(base (cell ?x ?y ?v))
(base (tried ?r ?x ?y))
(base (step ?s))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;; INIT Relations
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(init (cell 0 0 b))
(init (cell 0 1 b))
(init (cell 0 2 b))
(init (cell 1 0 b))
(init (cell 1 1 b))
(init (cell 1 2 b))
(init (cell 2 0 b))
(init (cell 2 1 b))
(init (cell 2 2 b))
(init (step 1))


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;; LEGAL Relations
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(<= (legal ?p (mark ?m ?n))
   (role ?p)
   (true (cell ?m ?n ?c))
   (distinct ?c ?p)
   (not (true (tried ?p ?m ?n))))

(<= doublehit
   (does white (mark ?m ?n))
   (does black (mark ?m ?n)))

(<= (validmove ?p ?m ?n)
   (does ?p (mark ?m ?n))
   (true (cell ?m ?n b))
   (not doublehit))

(<= (marked ?m ?n)
   (does ?p (mark ?m ?n))
   (not doublehit))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;; NEXT Relations
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(<= (next (cell ?m ?n ?p))
   (validmove ?p ?m ?n))

(<= (next (cell ?m ?n ?c))
   (true (cell ?m ?n ?c))
   (distinct ?c b))

(<= (next (cell ?m ?n b))
   (true (cell ?m ?n b))
   (not (marked ?m ?n)))

(<= (next (tried ?p ?m ?n))
   (does ?p (mark ?m ?n))
   (not (validmove ?p ?m ?n)))

(<= (next (tried ?p ?m1 ?n1))
   (true (tried ?p ?m1 ?n1))
   (does ?p (mark ?m2 ?n2))
   (not (validmove ?p ?m2 ?n2)))

(<= (next (step ?n))
   (true (step ?m))
   (succ ?m ?n))


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;; SEES Relations
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(<= (sees ?p (yougotit ?m ?n))
   (validmove ?p ?m ?n))

(<= (sees ?p (mark ?m ?n))
   (does ?p (mark ?m ?n))
   (not (validmove ?p ?m ?n)))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;; TERMINAL Relations
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(<= terminal
   (line white))

(<= terminal
   (line black))

(<= terminal
   (not open))

(<= terminal
   (true (step 30)))

(<= (line ?c)
   (true (cell ?m 0 ?c))
   (true (cell ?m 1 ?c))
   (true (cell ?m 2 ?c)))

(<= (line ?c)
   (true (cell 0 ?n ?c))
   (true (cell 1 ?n ?c))
   (true (cell 2 ?n ?c)))

(<= (line ?c)
   (true (cell 0 0 ?c))
   (true (cell 1 1 ?c))
   (true (cell 2 2 ?c)))

(<= (line ?c)
   (true (cell 0 2 ?c))
   (true (cell 1 1 ?c))
   (true (cell 2 0 ?c)))

(<= open
   (true (cell ?m1 ?n1 b))
   (not (true (tried white ?m1 ?n1)))
   (true (cell ?m2 ?n2 b))
   (not (true (tried black ?m2 ?n2))))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;; GOAL Relations
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(<= (goal white 100)
   (line white))
  
(<= (goal white 50)
   (not (line white))
   (not (line black)))
  
(<= (goal white 0)
   (line black))

(<= (goal black 100)
   (line black))
  
(<= (goal black 50)
   (not (line white))
   (not (line black)))
  
(<= (goal black 0)
   (line white))

(<= (tried ?r ?m  ?n)
    fail)